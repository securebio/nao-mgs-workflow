nextflow_process {

    name "Test process JOIN_TSVS"
    script "modules/local/joinTsvs/main.nf"
    process "JOIN_TSVS"
    config "tests/configs/run.config"
    tag "module"
    tag "join_tsvs"

    test("Should inner-join without errors on correct input and produce correct output"){
        tag "expect_success"
        when {
            params {}
            process {
                '''
                input[0] = Channel.of("test")
                    | combine(Channel.of("${projectDir}/test-data/toy-data/test_tab_sorted.tsv"))
                    | combine(Channel.of("${projectDir}/test-data/toy-data/test_tab_sorted_2.tsv"))
                input[1] = "x"
                input[2] = "inner"
                input[3] = "test"
                '''
            }
        }
        then {
            // Should run without errors
            assert process.success
            // Should have all columns from both inputs (and no others)
            def tab_in_1 = path(process.out.input[0][1]).csv(sep: "\t")
            def tab_in_2 = path(process.out.input[0][2]).csv(sep: "\t")
            def tab_out = path(process.out.output[0][1]).csv(sep: "\t", decompress: true)
            assert tab_out.columnCount == tab_in_1.columnCount + tab_in_2.columnCount - 1
            for (col in tab_in_1.columnNames) {
                assert col in tab_out.columnNames
            }
            for (col in tab_in_2.columnNames) {
                assert col in tab_out.columnNames
            }
            // Should have expected number of rows
            def ids_1 = tab_in_1.columns["x"]
            def ids_2 = tab_in_2.columns["x"]
            def match = 0
            for (i in ids_1){
                match += ids_2.count(i)
            }
            assert tab_out.rowCount == match
        }
    }

}
