nextflow_process {

    name "Test process KRAKEN"
    script "modules/local/kraken/main.nf"
    process "KRAKEN"
    config "tests/configs/run.config"
    tag "module"
    tag "kraken"

    test("Should run without failures and read counts should match") {
        tag "expect_success"
        tag "single_end"
        when {
            params {}
            process {
                '''
                input[0] = Channel.of("test")
                    | combine(Channel.of("${projectDir}/test-data/tiny-index/reads/R1.fastq"))
                input[1] = "${params.ref_dir}/results/kraken_db"
                input[2] = params.db_download_timeout
                '''
            }
        }
        then {
            // Should run without failures
            assert process.success
            // Output lines should match input reads
            def output_lines = path(process.out.output[0][1]).linesGzip.size()
            def input_reads  = path(process.out.input[0][1]).readLines().size() / 4
            assert output_lines == input_reads
        }
    }

    test("Should handle empty input file and produce empty output files") {
        tag "empty_file"
        tag "expect_success"
        when {
            params {}
            process {
                '''
                input[0] = Channel.of("test_empty")
                    | combine(Channel.of("${projectDir}/test-data/toy-data/empty_file.txt"))
                input[1] = "${params.ref_dir}/results/kraken_db"
                input[2] = params.db_download_timeout
                '''
            }
        }
        then {
            // Should run without failures
            assert process.success
            
            // Output files should be empty
            def countGzipLines = { file -> path(file).linesGzip.size() }
            
            // Check output is empty
            def output_lines = countGzipLines(process.out.output[0][1])
            assert output_lines == 0
            
            // Check report is empty (or contains only header)
            def report_lines = countGzipLines(process.out.report[0][1])
            assert report_lines <= 1
        }
    }
}
