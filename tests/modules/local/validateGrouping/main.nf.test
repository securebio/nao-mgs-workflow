nextflow_process {

    name "Test process VALIDATE_GROUPING"
    script "modules/local/validateGrouping/main.nf"
    process "VALIDATE_GROUPING"
    config "tests/configs/run.config"
    tag "module"
    tag "validate_grouping"

    test("Categorizes samples into validated, partial, and empty groups") {
        tag "expect_success"
        when {
            params {}
            process {
                '''
                // g1: S1 has hits, S2 no hits (partial)
                // g2: S3 no hits, S4 no hits (empty)
                // g3: S5 has hits (complete)
                input[0] = Channel.of(["test_label",
                    file("${projectDir}/test-data/validateGrouping/virus_hits_partial.tsv"),
                    file("${projectDir}/test-data/validateGrouping/grouping_mixed.tsv")])
                '''
            }
        }
        then {
            assert process.success

            // Check validated output - should have S1 and S5
            def validated = path(process.out.output[0][2]).csv(sep: "\t")
            assert validated.columns["sample"] as Set == ["S1", "S5"] as Set

            // Check partial group log - S2 should be here (g1 has S1 with hits)
            def partial = path(process.out.partial_group_log[0][1]).csv(sep: "\t")
            assert partial.columns["sample"] as Set == ["S2"] as Set

            // Check empty group log - S3 and S4 should be here (g2 has no hits)
            def empty_log = path(process.out.empty_group_log[0][1]).csv(sep: "\t")
            assert empty_log.columns["sample"] as Set == ["S3", "S4"] as Set
        }
    }

}
