nextflow_process {

    name "Test process EXTRACT_VIRAL_HITS_TO_FASTQ_NOREF_LABELED_LIST"
    script "modules/local/extractViralHitsToFastqNoref/main.nf"
    process "EXTRACT_VIRAL_HITS_TO_FASTQ_NOREF_LABELED_LIST"
    config "tests/configs/run.config"
    tag "module"
    tag "extract_viral_hits_to_fastq_noref_labeled_list"

    setup {
        run("LOAD_SAMPLESHEET") {
            script "subworkflows/local/loadSampleSheet/main.nf"
            process {
                '''
                input[0] = "${projectDir}/test-data/samplesheet.csv"
                input[1] = "illumina"
                input[2] = false
                '''
            }
        }
        run("EXTRACT_VIRAL_READS_SHORT", alias: "EXTRACT_VIRAL_READS_SHORT") {
            script "subworkflows/local/extractViralReadsShort/main.nf"
            process {
                '''
                def short_params = [
                    aln_score_threshold: params.bt2_score_threshold,
                    adapters: params.adapters,
                    cutadapt_error_rate: params.cutadapt_error_rate,
                    min_kmer_hits: "1",
                    k: "24",
                    bbduk_suffix: "viral",
                    taxid_artificial: "81077",
                    db_download_timeout: params.db_download_timeout
                ]
                input[0] = LOAD_SAMPLESHEET.out.samplesheet
                input[1] = params.ref_dir
                input[2] = short_params
                '''
            }
        }
    }

    test("Should process multiple TSV files in list format") {
        tag "list_processing"
        tag "expect_success"
        when {
            params {}
            process {
                '''
                input[0] = Channel.of("test")
                    .combine(EXTRACT_VIRAL_READS_SHORT.out.hits_final)
                    .map { sample, tsv ->
                        def tsv1 = file("${workDir}/partition_10239_${sample}_test.tsv.gz")
                        def tsv2 = file("${workDir}/partition_186538_${sample}_test.tsv.gz")
                        tsv.copyTo(tsv1)
                        tsv.copyTo(tsv2)
                        [sample, [tsv1, tsv2]]
                    }
                input[1] = false
                '''
            }
        }
        then {
            // Should run without failures
            assert process.success
            def output_fastqs = process.out.output[0][1]
            def input_tsvs = process.out.input[0][1]
            assert output_fastqs.size() == 2
            assert input_tsvs.size() == 2
            // Extract species IDs from input filenames and verify they're in output filenames
            def species_ids = input_tsvs.collect { tsv ->
                def matcher = tsv.toString() =~ /_(\d+)_hits/
                matcher ? matcher[0][1] : null
            }
            assert species_ids.every { it != null }
            assert output_fastqs.every { fastq ->
                species_ids.any { species -> fastq.toString().contains(species) }
            }
            def total_reads_in = 0
            def total_reads_out = 0
            for (int i = 0; i < 2; i++) {
                def tab_in = path(input_tsvs[i]).csv(sep: "\t", decompress: true)
                def fastq_out = path(output_fastqs[i]).fastq
                // Output should preserve read count
                def reads_in = tab_in.rowCount
                def reads_out = fastq_out.readNames.size()
                assert reads_in * 2 == reads_out
                // Read IDs and sequences should match
                def quals_out = fastq_out.qualityScores
                def seqs_out = fastq_out.sequences
                def seq_fwd
                def seq_rev
                for (int j = 0; j < reads_in; j++) {
                    assert tab_in.columns["seq_id"][j]+" 1" == fastq_out.readNames[2*j]
                    assert tab_in.columns["seq_id"][j]+" 2" == fastq_out.readNames[2*j+1]
                    seq_fwd = tab_in.columns["query_seq"][j]
                    seq_rev = tab_in.columns["query_seq_rev"][j]
                    if (seq_fwd == "") {
                        assert seqs_out[2*j] == "N"
                        assert quals_out[2*j] == "!"
                    } else {
                        assert seqs_out[2*j] == seq_fwd
                        assert quals_out[2*j] == tab_in.columns["query_qual"][j]
                    }
                    if (seq_rev == "") {
                        assert seqs_out[2*j+1] == "N"
                        assert quals_out[2*j+1] == "!"
                    } else {
                        assert seqs_out[2*j+1] == seq_rev
                        assert quals_out[2*j+1] == tab_in.columns["query_qual_rev"][j]
                    }
                }
                total_reads_in += reads_in
                total_reads_out += reads_out
            }
            assert total_reads_in * 2 == total_reads_out
        }
    }
}
