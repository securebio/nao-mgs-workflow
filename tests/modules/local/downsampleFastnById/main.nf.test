nextflow_process {

    name "Test process DOWNSAMPLE_FASTN_BY_ID"
    script "modules/local/downsampleFastnById/main.nf"
    process "DOWNSAMPLE_FASTN_BY_ID"
    config "tests/configs/run.config"
    tag "module"
    tag "downsample_fastn_by_id"

    test("Should correctly handle valid FASTQ input"){
        tag "expect_success"
        setup {
            run("GZIP_FILE") {
                script "modules/local/gzipFile/main.nf"
                process {
                    '''
                    input[0] = Channel.of("test")
                        | combine(Channel.of("${projectDir}/test-data/toy-data/test-random.fastq"))
                    '''
                }
            }
            run("SUBSET_FASTN") {
                script "modules/local/subsetFastn/main.nf"
                process {
                    '''
                    input[0] = GZIP_FILE.out
                    input[1] = 0.5
                    input[2] = "98765"
                    '''
                }
            }
            run("EXTRACT_FASTN_IDS") {
                script "modules/local/extractFastnIds/main.nf"
                process {
                    '''
                    input[0] = SUBSET_FASTN.out.output
                    '''
                }
            }
        }
        when {
            params {
            }
            process {
                '''
                input[0] = GZIP_FILE.out.combine(EXTRACT_FASTN_IDS.out.output, by: 0)
                '''
            }
        }
        then {
            // Process should run without errors
            assert process.success
            // FASTQ headers should match IDs
            def ids_in = path(process.out.input[0][2]).csv(header: false).columns["C0"]
            def fastq_out = path(process.out.output[0][1]).fastq
            assert ids_in == fastq_out.readNames.collect{it.tokenize(" ")[0]}.unique().toSorted()
            // FASTQ sequences should match corresponding sequences from input
            def fastq_in = path(process.out.input[0][1]).fastq
            for (name in fastq_out.readNames) {
                assert name in fastq_in.readNames
                def record_in = fastq_in.getRecordByName(name)
                def record_out = fastq_out.getRecordByName(name)
                assert record_in.getReadString() == record_out.getReadString()
                assert record_in.getBaseQualityString() == record_out.getBaseQualityString()
            }
            // REGRESSION TEST for issue #495: Verify output file is actually gzipped
            // When input is .gz, output must also be truly gzipped (not just named .gz)
            def output_file = process.out.output[0][1]
            // Check gzip magic bytes (0x1f 0x8b) at start of file
            def fis = new FileInputStream(new File(output_file.toString()))
            def firstByte = fis.read()
            def secondByte = fis.read()
            fis.close()
            assert firstByte == 0x1f : "File ${output_file} should be gzipped but first byte is ${String.format('0x%02x', firstByte)} instead of 0x1f"
            assert secondByte == 0x8b : "File ${output_file} should be gzipped but second byte is ${String.format('0x%02x', secondByte)} instead of 0x8b"
        }
    }

    test("Should correctly handle valid FASTA input"){
        tag "expect_success"
        setup {
            run("GZIP_FILE") {
                script "modules/local/gzipFile/main.nf"
                process {
                    '''
                    input[0] = Channel.of("test")
                        | combine(Channel.of("${projectDir}/test-data/toy-data/test-random.fasta"))
                    '''
                }
            }
            run("SUBSET_FASTN") {
                script "modules/local/subsetFastn/main.nf"
                process {
                    '''
                    input[0] = GZIP_FILE.out
                    input[1] = 0.5
                    input[2] = "98765"
                    '''
                }
            }
            run("EXTRACT_FASTN_IDS") {
                script "modules/local/extractFastnIds/main.nf"
                process {
                    '''
                    input[0] = SUBSET_FASTN.out.output
                    '''
                }
            }
        }
        when {
            params {
            }
            process {
                '''
                input[0] = GZIP_FILE.out.combine(EXTRACT_FASTN_IDS.out.output, by: 0)
                '''
            }
        }
        then {
            // Process should run without errors
            assert process.success
            // FASTA headers should match IDs
            def ids_in = path(process.out.input[0][2]).csv(header: false).columns["C0"]
            def fasta_out = path(process.out.output[0][1]).fasta
            assert ids_in == fasta_out.keySet().collect{it.tokenize(" ")[0]}.unique().toSorted()
            // FASTA sequences should match corresponding sequences from input
            def fasta_in = path(process.out.input[0][1]).fasta
            for (name in fasta_out.keySet()) {
                assert name in fasta_in.keySet()
                assert fasta_in[name] == fasta_out[name]
            }
            // REGRESSION TEST for issue #495: Verify output file is actually gzipped
            // When input is .gz, output must also be truly gzipped (not just named .gz)
            def output_file = process.out.output[0][1]
            // Check gzip magic bytes (0x1f 0x8b) at start of file
            def fis = new FileInputStream(new File(output_file.toString()))
            def firstByte = fis.read()
            def secondByte = fis.read()
            fis.close()
            assert firstByte == 0x1f : "File ${output_file} should be gzipped but first byte is ${String.format('0x%02x', firstByte)} instead of 0x1f"
            assert secondByte == 0x8b : "File ${output_file} should be gzipped but second byte is ${String.format('0x%02x', secondByte)} instead of 0x8b"
        }
    }

}
