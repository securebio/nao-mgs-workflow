nextflow_process {
    name "Test Process ADD_CONDITIONAL_TSV_COLUMN"
    script "modules/local/addConditionalTsvColumn/main.nf"
    process "ADD_CONDITIONAL_TSV_COLUMN"
    config "tests/configs/run.config"
    tag "process"
    tag "add_conditional_tsv_column"

    test("Should add conditional column based on column values") {
        tag "expect_success"
        when {
            params {
                column_params = [
                    chk_col: "x",
                    match_val: "0",
                    if_col: "y",
                    else_col: "z",
                    new_hdr: "selected"
                ]
            }
            process {
                """
                input[0] = ["sample1", file("${projectDir}/test-data/toy-data/test_tab_single.tsv")]
                input[1] = params.column_params
                """
            }
        }
        then {
            assert process.success // Basic success test
            def inputData = path("${projectDir}/test-data/toy-data/test_tab_single.tsv").csv(sep: "\t", decompress: false)
            def outputData = path(process.out.tsv[0][1]).csv(sep: "\t", decompress: false)
            assert outputData.rowCount == inputData.rowCount
            assert outputData.columnCount == inputData.columnCount + 1
            assert params.column_params.new_hdr in outputData.columns.keySet()
            for (int i = 0; i < outputData.rowCount; i++) {
                def xValue = outputData.columns[params.column_params.chk_col][i]
                def yValue = outputData.columns[params.column_params.if_col][i]
                def zValue = outputData.columns[params.column_params.else_col][i]
                def selectedValue = outputData.columns[params.column_params.new_hdr][i]
                if (xValue == 0) {
                    assert selectedValue == yValue
                } else {
                    assert selectedValue == zValue
                }
            }
        }
    }

    test("Should preserve quote characters without CSV escaping") {
        // Regression test for FASTQ quality strings containing " (ASCII 34 = Phred+33 quality 1)
        // CSV escaping would double quotes and wrap fields, corrupting quality string lengths
        tag "expect_success"
        tag "quote_regression"
        when {
            params {
                column_params = [
                    chk_col: "x",
                    match_val: "0",
                    if_col: "y",
                    else_col: "z",
                    new_hdr: "selected"
                ]
            }
            process {
                """
                input[0] = ["sample1", file("${projectDir}/test-data/addConditionalTsvColumn/test_tab_with_quotes.tsv")]
                input[1] = params.column_params
                """
            }
        }
        then {
            assert process.success
            def outputLines = path(process.out.tsv[0][1]).readLines()
            assert outputLines.size() == 4
            // Header with new column
            assert outputLines[0] == 'x\ty\tz\tselected'
            // Row 1: x=0, so selected=y (AAAA"BBBB)
            assert outputLines[1] == '0\tAAAA"BBBB\tother\tAAAA"BBBB'
            // Row 2: x=1, so selected=z (CCCC"DDDD)
            assert outputLines[2] == '1\tno_quotes\tCCCC"DDDD\tCCCC"DDDD'
            // Row 3: x=0, so selected=y (EE"FF"GG)
            assert outputLines[3] == '0\tEE"FF"GG\tmore\tEE"FF"GG'
        }
    }
}
