nextflow_process {

    name "Test process VSEARCH_CLUSTER_LIST"
    script "modules/local/vsearch/main.nf"
    process "VSEARCH_CLUSTER_LIST"
    config "tests/configs/run.config"
    tag "module"
    tag "vsearch_list"

    setup {
        run("LOAD_SAMPLESHEET") {
            script "subworkflows/local/loadSampleSheet/main.nf"
            process {
                """
                input[0] = "${projectDir}/test-data/single-end-samplesheet.csv"
                input[1] = "illumina"
                input[2] = true
                """
            }
        }
        run("COPY_FILE") {
            script "modules/local/copyFile/main.nf"
            process {
                """
                input[0] = LOAD_SAMPLESHEET.out.samplesheet
                input[1] = "input.fastq.gz"
                """
            }
        }
    }

    test("Should process multiple FASTQ files in list format"){
        tag "list_processing"
        tag "expect_success"
        when {
            params {}
            process {
                '''
                input[0] = COPY_FILE.out
                    .map { sample, fastq ->
                        def orig = file(fastq)
                        def f1 = file("${workDir}/${sample}_10239_joined.fastq.gz")
                        def f2 = file("${workDir}/${sample}_186538_joined.fastq.gz")
                        orig.copyTo(f1)
                        orig.copyTo(f2)
                        [sample, [f1, f2]]
                    }
                input[1] = 0.95
                input[2] = 0
                input[3] = 15
                '''
            }
        }
        then {
            // Should run without failures
            assert process.success
            def output_reps = process.out.reps[0][1]
            def output_summaries = process.out.summary[0][1]
            def output_logs = process.out.log[0][1]
            def input_files = process.out.input[0][1]
            assert output_reps.size() == 2
            assert output_summaries.size() == 2
            assert output_logs.size() == 2
            assert input_files.size() == 2
            // Extract species IDs from input filenames and verify they're in output filenames
            def species_ids = input_files.collect { input ->
                def matcher = input.toString() =~ /input_.*?_(\d+)_joined/
                matcher ? matcher[0][1] : null
            }
            assert species_ids.every { it != null }
            assert output_reps.every { output ->
                species_ids.any { species -> output.toString().contains(species) }
            }
            assert output_summaries.every { output ->
                species_ids.any { species -> output.toString().contains(species) }
            }
            assert output_logs.every { output ->
                species_ids.any { species -> output.toString().contains(species) }
            }
            // Outputs should be compatible and match input
            for (int i = 0; i < 2; i++) {
                def seq_in = path(input_files[i]).fastq
                def tab_out = path(output_summaries[i]).csv(sep: "\t", decompress: true, header: false)
                def seq_out = path(output_reps[i]).fasta
                def n_clusters = tab_out.columns["C0"].count("C")
                def n_reps = tab_out.columns["C0"].count("S")
                def n_hits = tab_out.columns["C0"].count("H")
                def n_seqs = n_reps + n_hits
                assert seq_in.sequences.size() == n_seqs
                assert seq_out.size() == n_reps
                assert n_reps == n_clusters
            }
        }
    }
}
