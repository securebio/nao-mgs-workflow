nextflow_process {

    name "Test process FILTER_BLAST"
    script "modules/local/filterBlast/main.nf"
    process "FILTER_BLAST"
    config "tests/configs/run.config"
    tag "module"
    tag "filter_blast"

    test("Should run without failures on correct input") {
        tag "expect_success"
        tag "interleaved"
        when {
            params {
                max_rank = 10
                min_frac = 0.9
            }
            process {
                '''
                input[0] = Channel.of("test")
                    | combine(Channel.of("${projectDir}/test-data/toy-data/filter-blast/input.tsv"))
                input[1] = params.max_rank
                input[2] = params.min_frac
                '''
            }
        }
        then {
            // Should run without failures
            assert process.success
            // Output TSV should have expected columns
            def tab_in = path(process.out.input[0][1]).csv(sep: "\t", header: false)
            def tab_out = path(process.out.output[0][1]).csv(sep: "\t", decompress: true)
            assert tab_in.columnCount + 2 == tab_out.columnCount
            def cols_out_exp = ["qseqid", "sseqid", "sgi", "staxid", "qlen", "evalue",
                         "bitscore", "qcovs", "length", "pident", "mismatch",
                         "gapopen", "sstrand", "qstart", "qend", "sstart", "send",
                         "bitscore_rank_dense", "bitscore_fraction"]
            assert tab_out.columnNames == cols_out_exp
            // Output TSV should have fewer rows than input
            assert tab_in.rowCount > tab_out.rowCount
            // All output lines should meet score and/or rank criteria
            def meets_rank = false
            def meets_frac = false
            for (r in tab_out.rows){
                meets_rank = r["bitscore_rank_dense"] <= params.max_rank
                meets_frac = r["bitscore_fraction"] >= params.min_frac
                assert meets_rank && meets_frac
            }
            // TODO: Add object-level test of correct filtering
        }
    }

}
