nextflow_workflow {

    name "Test workflow DISCOVER_RUN_OUTPUT"
    script "subworkflows/local/discoverRunOutput/main.nf"
    workflow "DISCOVER_RUN_OUTPUT"
    config "tests/configs/downstream.config"
    tag "subworkflow"
    tag "downstream"
    tag "discover_run_output"

    test("Should discover all files and match to samples") {
        tag "expect_success"
        when {
            params {
            }
            workflow {
                '''
                // Simulate run_dirs channel: tuple(label, resolved_run_results_dir)
                input[0] = Channel.of(
                    ["tt1", "${projectDir}/test-data/results/run_output_shortread/"]
                )
                // Simulate groups channel: tuple(label, sample, group)
                input[1] = Channel.of(
                    ["tt1", "tiny_test", "group_a"]
                )
                '''
            }
        }
        then {
            assert workflow.success
            // Should discover all TSV files matching the sample prefix
            assert workflow.out.output.size() > 0

            // Verify tuple structure: (label, sample, file, group)
            workflow.out.output.each { output ->
                assert output.size() == 4
                assert output[0] == "tt1"       // label
                assert output[1] == "tiny_test" // sample
                assert output[3] == "group_a"   // group
                // All filenames should start with the sample name
                def filename = path(output[2]).getFileName().toString()
                assert filename.startsWith("tiny_test_")
            }

            // Verify specific expected files are present
            def filenames = workflow.out.output.collect { path(it[2]).getFileName().toString() } as Set
            assert "tiny_test_virus_hits.tsv" in filenames
            assert "tiny_test_read_counts.tsv" in filenames
        }
    }

    test("Should return empty channel when sample has no files") {
        tag "expect_success"
        when {
            params {
            }
            workflow {
                '''
                input[0] = Channel.of(
                    ["tt1", "${projectDir}/test-data/results/run_output_shortread/"]
                )
                // Use a sample name that doesn't match any file prefixes
                input[1] = Channel.of(
                    ["tt1", "nonexistent_sample", "group_a"]
                )
                '''
            }
        }
        then {
            assert workflow.success
            assert workflow.out.output.size() == 0
        }
    }
}
