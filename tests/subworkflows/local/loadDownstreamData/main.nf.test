nextflow_workflow {

    name "Test Workflow LOAD_DOWNSTREAM_DATA"
    script "subworkflows/local/loadDownstreamData/main.nf"
    workflow "LOAD_DOWNSTREAM_DATA"
    config "tests/configs/downstream.config"
    tag "load_downstream_data"
    tag "subworkflow"
    tag "downstream"

    test("Should run without failures on valid input") {
        tag "expect_success"
        when {
            params {}
            workflow {
                """
                input[0] = file("${projectDir}/test-data/downstream/input_file.csv")
                """
            }
        }
        then {
            assert workflow.success
            // Parse input CSV from workflow output
            def inputCsv = path(workflow.out.test_input[0]).csv
            def expectedLabels = inputCsv.columns["label"] as Set
            // Verify hits output: tuple(label, sample, hits_file, group)
            assert workflow.out.hits.size() > 0
            def hitLabels = workflow.out.hits.collect { it[0] } as Set
            assert hitLabels == expectedLabels, "Should have hits for all labels in input"
            workflow.out.hits.each { hit ->
                assert hit.size() == 4, "hits tuple should have 4 elements"
                def label = hit[0]
                def sample = hit[1]
                def hits_file = hit[2]
                def group = hit[3]
                assert expectedLabels.contains(label)
                def filename = hits_file.toString().split('/').last()
                assert filename.endsWith("_virus_hits.tsv.gz")
                assert sample == filename.replace("_virus_hits.tsv.gz", "")
                assert group != null && group.length() > 0, "Group should not be empty"
            }
            // Verify missing_groups is empty when all samples have hits
            assert workflow.out.missing_groups[0].size() == 0, "No groups should be missing"
            assert workflow.out.start_time_str != null
        }
    }

    test("Should identify missing groups") {
        tag "expect_success"
        tag "missing_groups"
        when {
            params {}
            workflow {
                """
                input[0] = file("${projectDir}/test-data/downstream/input_file_missing_groups.csv")
                """
            }
        }
        then {
            assert workflow.success
            // Verify missing_groups contains expected groups
            def missingGroups = workflow.out.missing_groups[0]
            assert missingGroups.size() > 0, "Should have missing groups"
            assert missingGroups.contains("missing_group"), "Should identify 'missing_group' as missing"
        }
    }

    test("Should fail on invalid input file") {
        tag "expect_failed"
        when {
            params {}
            workflow {
                """
                input[0] = file("${projectDir}/test-data/downstream/input_file_invalid.csv")
                """
            }
        }

        then {
            assert workflow.failed
            assert workflow.exitStatus == 1
            assert workflow.stdout.any { it.contains("Invalid input header") }
        }
    }

}
