nextflow_workflow {

    name "Test Workflow LOAD_DOWNSTREAM_DATA"
    script "subworkflows/local/loadDownstreamData/main.nf"
    workflow "LOAD_DOWNSTREAM_DATA"
    config "tests/configs/downstream.config"
    tag "load_downstream_data"
    tag "subworkflow"
    tag "downstream"

    test("Should run without failures on valid input") {
        tag "expect_success"
        when {
            params {}
            workflow {
                """
                input[0] = file("${projectDir}/test-data/downstream/input_file.csv")
                input[1] = projectDir
                """
            }
        }
        then {
            // Should run without errors
            assert workflow.success
            // Should produce correctly-structured output
            def tab_in = path(workflow.out.test_input[0]).csv
            assert workflow.out.input.size() == tab_in.rowCount
            for (int i=0; i < tab_in.rowCount; i++){
                assert workflow.out.input[i][0] == tab_in.columns["label"][i]
                // Hits file is a concatenated result from directory discovery
                assert workflow.out.input[i][1].toString().endsWith("_virus_hits_combined.tsv.gz")
                // Resolved paths should end with the relative paths from CSV
                assert workflow.out.input[i][2].toString().endsWith(tab_in.columns["groups_tsv"][i])
            }
        }
    }

    test("Should fail on empty run_results_dir") {
        tag "expect_failed"
        when {
            params {}
            workflow {
                """
                input[0] = file("${projectDir}/test-data/downstream/input_file_empty_results_dir.csv")
                input[1] = projectDir
                """
            }
        }

        then {
            assert workflow.failed
            assert workflow.stdout.any { it.contains("Missing or empty 'run_results_dir'") }
        }
    }

    test("Should fail on invalid input file") {
        tag "expect_failed"
        when {
            params {}
            workflow {
                """
                input[0] = file("${projectDir}/test-data/downstream/input_file_invalid.csv")
                input[1] = projectDir
                """
            }
        }

        then {
            assert workflow.failed
            assert workflow.exitStatus == 1
            assert workflow.stdout.any { it.contains("Invalid input header") }
        }
    }

}
