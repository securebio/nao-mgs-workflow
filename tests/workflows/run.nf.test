import static groovy.io.FileType.FILES

// Get expected output files from pyproject.toml
// Replaces {SAMPLE} and {GROUP} wildcards with the provided name
def getExpectedOutputs = { pyprojectPath, workflowKey, sampleOrGroupName ->
    def pyprojectText = pyprojectPath.text
    def key = "expected-outputs-${workflowKey}"
    def sectionMatch = (pyprojectText =~ /${key} = \[([\s\S]*?)\]/)
    def expectedFiles = (sectionMatch[0][1] =~ /"([^"]+)"/).collect { it[1] }
    if (sampleOrGroupName != null) {
        expectedFiles = expectedFiles.collect {
            it.replace("{GROUP}", sampleOrGroupName).replace("{SAMPLE}", sampleOrGroupName)
        }
    }
    return expectedFiles.sort()
}

// Get observed output files, excluding intermediates and trace files
def getObservedOutputs = { outputDir ->
    def excludePatterns = ["intermediates", "trace"]
    def observedFiles = []
    outputDir.eachFileRecurse(FILES) { file ->
        def relPath = outputDir.relativize(file).toString()
        def excluded = excludePatterns.any { pattern -> relPath.contains(pattern) }
        if (!excluded) observedFiles << relPath
    }
    return observedFiles.sort()
}

// Get result file paths for snapshot from expected outputs
def getResultPaths = { outputDir, expectedFiles ->
    return expectedFiles
        .findAll { it.startsWith("results/") }
        .collect { path(outputDir + "/" + it) }
}

nextflow_pipeline {

    name "Test workflow RUN"
    script "main.nf" // Running from main so we have access to published files
    tag "workflow"
    
    test("Should handle empty input files") {
        config "tests/configs/run.config"
        tag "empty_input"
        tag "expect_success"
        when {
            params {
                // Use the empty sample sheet that references empty S3 files
                sample_sheet = "${projectDir}/test-data/empty-samplesheet.csv"
            }
        }
        then {
            // Should run without failures
            assert workflow.success
        }
    }

    test("Short read RUN workflow output should match snapshot") {
        config "tests/configs/run.config"
        tag "run_output"
        tag "main_run_output_shortread"
        then {
            assert workflow.success
            // Verify expected outputs match pyproject.toml (sample name: tiny_test)
            def expected = getExpectedOutputs(path("${projectDir}/pyproject.toml"), "run", "tiny_test")
            def observed = getObservedOutputs(path("${launchDir}/output"))
            assert observed == expected : "Expected ${expected} but got ${observed}"
            // Verify result file contents match snapshot
            def resultPaths = getResultPaths("${launchDir}/output", expected)
            assert snapshot(*resultPaths).match("run_output_shortread")
        }
    }

    test("ONT RUN workflow output should match snapshot") {
        config "tests/configs/run_ont.config"
        tag "run_output_ont"
        tag "main_run_output_ont"
        then {
            assert workflow.success
            // Verify expected outputs match pyproject.toml (sample name: tiny_test)
            def expected = getExpectedOutputs(path("${projectDir}/pyproject.toml"), "run", "tiny_test")
            def observed = getObservedOutputs(path("${launchDir}/output"))
            assert observed == expected : "Expected ${expected} but got ${observed}"
            // Verify result file contents match snapshot
            def resultPaths = getResultPaths("${launchDir}/output", expected)
            assert snapshot(*resultPaths).match("run_output_ont")
        }
    }
}
