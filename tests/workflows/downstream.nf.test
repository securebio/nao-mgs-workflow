import static groovy.io.FileType.FILES

// Get expected output files from pyproject.toml
// Replaces {SAMPLE} and {GROUP} wildcards with the provided name
def getExpectedOutputs = { pyprojectPath, workflowKey, sampleOrGroupName ->
    def pyprojectText = pyprojectPath.text
    def key = "expected-outputs-${workflowKey}"
    def sectionMatch = (pyprojectText =~ /${key} = \[([\s\S]*?)\]/)
    def expectedFiles = (sectionMatch[0][1] =~ /"([^"]+)"/).collect { it[1] }
    if (sampleOrGroupName != null) {
        expectedFiles = expectedFiles.collect {
            it.replace("{GROUP}", sampleOrGroupName).replace("{SAMPLE}", sampleOrGroupName)
        }
    }
    return expectedFiles.sort()
}

// Get observed output files, excluding intermediates and trace files
def getObservedOutputs = { outputDir ->
    def excludePatterns = ["intermediates", "intermediates_downstream", "trace"]
    def observedFiles = []
    outputDir.eachFileRecurse(FILES) { file ->
        def relPath = outputDir.relativize(file).toString()
        def excluded = excludePatterns.any { pattern -> relPath.contains(pattern) }
        if (!excluded) observedFiles << relPath
    }
    return observedFiles.sort()
}

// Get result file paths for snapshot from expected outputs
def getResultPaths = { outputDir, expectedFiles ->
    return expectedFiles
        .findAll { it.startsWith("results_downstream/") }
        .collect { path(outputDir + "/" + it) }
}

nextflow_pipeline {

    name "Test workflow DOWNSTREAM"
    script "main.nf" // Running from main so we have access to published files
    config "tests/configs/downstream.config"
    tag "workflow"

    test("DOWNSTREAM workflow output should match snapshot") {
        tag "downstream_output"
        tag "main_downstream_output"
        tag "snapshot"
        then {
            assert workflow.success
            // Verify expected outputs match pyproject.toml
            def expected = getExpectedOutputs(path("${projectDir}/pyproject.toml"), "downstream", "tt1")
            def observed = getObservedOutputs(path("${launchDir}/output"))
            assert observed == expected : "Expected ${expected} but got ${observed}"
            // Verify result file contents match snapshot
            def resultPaths = getResultPaths("${launchDir}/output", expected)
            assert snapshot(*resultPaths).match("downstream_output")
        }
    }

    test("ONT DOWNSTREAM workflow output should match snapshot") {
        config "tests/configs/downstream_ont.config"
        tag "downstream_output_ont"
        tag "main_downstream_output_ont"
        tag "snapshot_ont"
        then {
            assert workflow.success
            // Verify expected outputs match pyproject.toml
            def expected = getExpectedOutputs(path("${projectDir}/pyproject.toml"), "downstream-ont", "tt1")
            def observed = getObservedOutputs(path("${launchDir}/output"))
            assert observed == expected : "Expected ${expected} but got ${observed}"
            // Verify result file contents match snapshot
            def resultPaths = getResultPaths("${launchDir}/output", expected)
            assert snapshot(*resultPaths).match("downstream_ont_output")
        }
    }

    test("Should handle header-only input files") {
        tag "downstream_empty_input"
        tag "empty_input"
        when {
            params {
                input_file = "${projectDir}/test-data/downstream/input_file_empty_only.csv"
            }
        }
        then {
            assert workflow.success
            // Verify expected outputs match pyproject.toml
            def expected = getExpectedOutputs(path("${projectDir}/pyproject.toml"), "downstream", "empty_group")
            def observed = getObservedOutputs(path("${launchDir}/output"))
            assert observed == expected : "Expected ${expected} but got ${observed}"
        }
    }

    test("Should create placeholder files for missing groups") {
        tag "downstream_missing_groups"
        tag "missing_groups"
        when {
            params {
                input_file = "${projectDir}/test-data/downstream/input_file_missing_groups.csv"
            }
        }
        then {
            assert workflow.success
            // Verify expected outputs match pyproject.toml
            def expected = getExpectedOutputs(path("${projectDir}/pyproject.toml"), "downstream", "missing_group")
            def observed = getObservedOutputs(path("${launchDir}/output"))
            assert observed == expected : "Expected ${expected} but got ${observed}"
        }
    }
}
